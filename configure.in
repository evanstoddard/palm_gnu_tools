dnl Process this file with autoconf to produce a configure script.
AC_INIT(prc-tools.spec)

AC_CONFIG_SUBDIRS(binutils gdb gcc make)

AC_CANONICAL_SYSTEM

AC_PROG_CC
AC_PROG_CXX
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_RANLIB
AC_PROG_LEX
AC_PROG_YACC
AC_EXEEXT

AC_ARG_WITH(build-sdk,
[  --with-build-sdk=FLAGS  which SDK to build runtime [internal bootstrap SDK]],
[case "${withval}" in
yes)	build_sdk="-palmos" ;;
no)	;;
*)	build_sdk=$with_build_sdk ;;
esac])

if test x$build_sdk = x; then
  build_sdk='$(BOOTSTRAP_SDKFLAGS)'
fi

AC_SUBST(build_sdk)

AC_ARG_WITH(shared-libstdcxx-for-tools,
[  --without-shared-libstdcxx-for-tools
                          try not to use shared libstdc++ in build-prc [with]],
[case "${withval}" in
no)	static_cxxlibs='$(STATIC_CXXLIBS)' ;;
*)	;;
esac])
AC_SUBST(static_cxxlibs)

AC_MSG_CHECKING([prc-tools package version])
if test -f $srcdir/prc-tools.spec; then
  prc_tools_version=`grep ^Version: $srcdir/prc-tools.spec | sed 's/.*: //'`
else
  prc_tools_version=unknown
fi
AC_MSG_RESULT($prc_tools_version)
AC_SUBST(prc_tools_version)

AC_OUTPUT(Makefile crt/Makefile crt/Make-multi doc/Makefile include/Makefile libc/Makefile libm/Makefile tools/Makefile)
