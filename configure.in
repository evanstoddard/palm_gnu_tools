dnl Autoconf configure script for prc-tools.
dnl
dnl Copyright 2000, 2001, 2002 John Marshall.
dnl
dnl This is free software; you can redistribute it and/or modify
dnl it under the terms of the GNU General Public License as published by
dnl the Free Software Foundation; either version 2, or (at your option)
dnl any later version.
dnl
dnl Process this file with autoconf to produce a configure script.

AC_PREREQ(2.13)
AC_INIT(prc-tools.spec)


palmdev_prefix=/opt/palmdev

AC_ARG_WITH(palmdev-prefix,
[  --with-palmdev-prefix=DIR
                          where to look for SDKs [/opt/palmdev]],
[case "${withval}" in
yes|no)	AC_MSG_ERROR(bad value ${withval} given for palmdev prefix) ;;
*)	palmdev_prefix="${withval}" ;;
esac])

AC_SUBST(palmdev_prefix)


build_sdk='$(BOOTSTRAP_SDKFLAGS)'

AC_ARG_WITH(build-sdk,
[  --with-build-sdk=FLAGS  which SDK to build runtime [internal bootstrap SDK]],
[case "${withval}" in
yes)	build_sdk="-palmos" ;;
no)	;;
*)	build_sdk=$with_build_sdk ;;
esac])

AC_SUBST(build_sdk)


htmldocs_prefix='$(palmdev_prefix)/doc'
htmldocs_enabled=''
install_htmldocs_enabled=''

AC_ARG_ENABLE(html-docs,
[  --enable-html-docs[=DIR]where to install HTML versions of the documentation
                          [disabled; PALMDEV_PREFIX/doc]],
[case "${enableval}" in
yes)	htmldocs_enabled=html install_htmldocs_enabled=install-html ;;
no)	;;
*)	htmldocs_prefix=${enableval}
	htmldocs_enabled=html install_htmldocs_enabled=install-html ;;
esac])

AC_SUBST(htmldocs_prefix)
AC_SUBST(htmldocs_enabled)
AC_SUBST(install_htmldocs_enabled)


AC_CANONICAL_SYSTEM

AC_ARG_ENABLE(generic,
[  --disable-generic       omit tools etc that are not target-specific])

config_subdirs=""
output_files="Makefile"

if false
  # FIXME handle "top level configury"
else
  case "$target_cpu" in
  m68k)	config_subdirs="$config_subdirs binutils gcc gdb"
  	output_files="$output_files crt/Makefile libc/Makefile libm/Makefile"
	;;
  arm)	config_subdirs="$config_subdirs binutils gcc"
  	output_files="$output_files"
	;;
  *)	AC_MSG_ERROR($target is not supported as a Palm OS target)
  	;;
  esac
fi

if test x$enable_generic != xno; then
  config_subdirs="$config_subdirs make"
  output_files="$output_files doc/Makefile doc/version.texi include/Makefile
		tools/Makefile"
fi


AC_PROG_CC
AC_PROG_CXX
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_RANLIB
AC_PROG_LEX
AC_PROG_YACC
AC_EXEEXT

AC_CHECK_PROG(TEXI2HTML, texi2html, texi2html, false)
if test -n "$htmldocs_enabled"; then
  # Check that texi2html understands its new-style (e.g. -out_file) arguments
  if test $TEXI2HTML != false &&
     $TEXI2HTML -out_file /dev/null /dev/null >/dev/null 2>/dev/null
  then :
  else
    AC_MSG_ERROR(modern texi2html needed to build HTML documentation)
  fi
fi

AC_MSG_CHECKING([prc-tools package version])
if test -f $srcdir/prc-tools.spec; then
  prc_tools_version=`grep '^Version:' $srcdir/prc-tools.spec | sed 's/.*: //'`
else
  prc_tools_version=unknown
fi
AC_MSG_RESULT($prc_tools_version)
AC_SUBST(prc_tools_version)

AC_CONFIG_SUBDIRS($config_subdirs)
AC_OUTPUT($output_files)
